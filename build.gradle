plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco' // JaCoCo 플러그인 적용
}

group = 'com.know-who-how'
version = '0.0.1-SNAPSHOT'
description = '노후하우 메인 서버'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.5'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'com.solapi:sdk:1.0.3'

	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
}

// JaCoCo 설정
jacoco {
    toolVersion = "0.8.12" // 사용할 JaCoCo 버전 명시
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // 'test' 작업이 성공적으로 끝나면 항상 jacocoTestReport 작업을 실행
}

// JaCoCo 리포트 생성 작업에 대한 설정
jacocoTestReport {
    dependsOn test
    reports {
        html.required = true    // HTML 형식의 리포트를 생성
        xml.required = false
        csv.required = false
    }

    // 특정 파일들을 커버리지 측정에서 제외함
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/dto/**',
                '**/entity/**',
                '**/config/**',
                '**/exception/**',
                '**/jwt/**',
                '**/*Application*', // 메인 애플리케이션 클래스 제외
            ])
        }))
    }
}
